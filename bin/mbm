#!/usr/bin/perl

use strict;
use warnings;
use feature "state", "say";
use Getopt::Long;
use Mojo::UserAgent;
use Mojo::DOM;
use Mojo::JSON 'decode_json';
use Mojo::ByteStream 'b';
use Mojo::Collection 'c';
use Path::Tiny;

my $base = path('~/.mbm/');
$base->mkpath();

my $config = {
    stopwords => [
        qw(a about above after again against all am an and any are aren't as at be because been before being below between both but by can can't cannot could couldn't did didn't do does doesn't doing don't down during each few for from further had hadn't has hasn't have haven't having he he'd he'll he's her here here's hers herself him himself his how how's i i'd i'll i'm i've if in into is isn't it it's its itself let's me more most mustn't my myself no nor not of off on once only or other ought our ours ourselves out over own same shan't she she'd she'll she's should shouldn't so some such than that that's the their theirs them themselves then there there's these they they'd they'll they're they've this those through to too under until up use very was wasn't we we'd we'll we're we've were weren't what what's when when's where where's which while who who's whom why why's will with won't would wouldn't you you'd you'll you're you've your yours yourself yourselves)
    ],
    handler => [qw(iceweasel -new-tab)],
    worker  => 5,
};

my $config_file = $base->child('mbmrc');
if ( $config_file->exists ) {
    my $json = eval { decode_json( $config_file->slurp_utf8 ) };
    if ($json) {
        $config = { %$config, %$json };
    }
}

for (qw(title content url)) {
    $base->child($_)->mkpath();
}

my $mode = shift;

if ( $mode eq 'add' ) {
    my $url = shift;
    add_urls( [$url] );
}

elsif ( $mode eq 'import' ) {
    my $file    = shift;
    my $content = path($file)->slurp_utf8;
    my $dom     = Mojo::DOM->new($content);
    my $urls =
      $dom->find('a["href"]')->map( attr => 'href' )->grep( sub { /^http/ } )
      ->to_array;
    add_urls($urls);
}
elsif ( $mode eq 'tag' ) {
    my $url  = shift;
    my $id   = b($url)->sha1_sum;
    my @tags = split( ' ', db_get( $base, $id, 'tag' ) );
}
elsif ( $mode eq 'open' ) {
	my $num = shift;
	my @urls = $base->child('mbm_last_search')->lines();
	system(@{$config->{handler}},$urls[$num-1]);
}
elsif ( $mode eq 'search' ) {
    my @terms = @ARGV ? @ARGV : '';
    my $matches = db_search( $base, [qw(title url content)], $terms[0] );
    my $hist_fh = $base->child('mbm_last_search')->openw_utf8();

    my $i = 0;
    my $len = length(@$matches);
    for my $id (@$matches) {
        my $url   = db_get( $base, $id, 'url' );
        my $title = db_get( $base, $id, 'title' );
        my $line  = sprintf("%*d $url - $title",$len,++$i);
        print b($line)->encode . "\n";
	print {$hist_fh} "$url\n";
    }
}

sub db_keys {
    my ($base) = @_;
    return map { $_->basename } $base->child('url')->children;
}

sub db_get {
    my ( $base, $id, $prop ) = @_;
    return $base->child($prop)->child($id)->slurp_utf8;
}

sub db_set {
    my ( $base, $id, $prop, $content ) = @_;
    return $base->child($prop)->child($id)->spew_utf8($content);
}

sub db_search {
    my ( $base, $props, $term ) = @_;
    local ( @ARGV, $_ ) = map { $base->child($_)->children() } @$props;
    return if !@ARGV;
    my @matches;
    while (<>) {
        if (/$term/o) {
            push @matches, $ARGV;
            close ARGV;
            next;
        }
    }
    return c(@matches)->map('basename')->uniq->to_array;
}

sub add_urls {
    my $urls = shift;
    state $ua    = Mojo::UserAgent->new()->max_redirects(5);
    state $idle  = $config->{worker};
    state $delay = Mojo::IOLoop->delay();

    while ( $idle and my $url = shift @$urls ) {
        $idle--;
        my $cb = $delay->begin;
        $ua->get(
            $url => sub {
                my ( $ua, $tx ) = @_;
                $idle++;
                process_tx( $tx, $url );

                # refresh worker pool
                add_urls($urls);
                $cb->();
            }
        );
    }
    $delay->wait unless $delay->ioloop->is_running;
}

sub process_tx {
    my ( $tx, $url ) = @_;
    if ( my $res = $tx->success ) {
        my ( $title, $content );
        my $dom = $res->dom;
        if ($dom) {
            $title = $dom->at('title');
            if ($title) {
                $title = b( $title->all_text() )->squish;
            }
            $dom->find('head')->map('remove');
            $dom->find('script')->map('remove');
            my %stopword =
              map { $_ => 1 } @{ $config->{stopwords} };

            ## There are many pages without a body-tag, so i just use all the
            ## text in the dom as content, TODO remove head before all_text()?

            $content = $dom->all_text();
            if ($content) {
                $content = b($content)->squish->split(' ')
                  ->uniq->grep( sub { not exists $stopword{$_} } )->join(' ');
            }
            else {
                warn "No words for $url\n";
            }
        }
        my $id = b($url)->sha1_sum;

        db_set( $base, $id, 'title',   $title   || '' );
        db_set( $base, $id, 'content', $content || '' );
        db_set( $base, $id, 'url',     $url );
    }
    else {
        my $err = $tx->error;
        print STDERR "Error for $url: ";
        if ( $err->{code} ) {
            warn "code $err->{code} response: $err->{message}\n";
        }
        else {
            warn "Connection error: $err->{message}\n";
        }

    }
}

exit 0;

__END__

=pod
